<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="utf-8">
  <meta name="color-scheme" content="light dark">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <title>08-awk.sh</title>
  <link rel="stylesheet" href="../style/code.css">
  <link rel="stylesheet" href="../highlightjs/github.min.css" media="screen and (prefers-color-scheme: light)">
  <link rel="stylesheet" href="../highlightjs/github-dark.min.css" media="screen and (prefers-color-scheme: dark)">
  <script src="../highlightjs/highlight-bash.min.js"></script>
  <script>hljs.highlightAll();</script>
</head>

<body>

<pre><code class="language-bash">#!/bin/sh
# see also `man awk`

## ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
## AWK
## ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

# awk (1) - pattern scanning and text processing language

# print each line (implicit)
awk &apos;{ print }&apos; /etc/passwd | head
# root:x:0:0:root:/root:/bin/bash
# daemon:x:1:1:daemon:/usr/sbin:/usr/sbin/nologin
# bin:x:2:2:bin:/bin:/usr/sbin/nologin
# sys:x:3:3:sys:/dev:/usr/sbin/nologin
# sync:x:4:65534:sync:/bin:/bin/sync
# games:x:5:60:games:/usr/games:/usr/sbin/nologin
# man:x:6:12:man:/var/cache/man:/usr/sbin/nologin
# lp:x:7:7:lp:/var/spool/lpd:/usr/sbin/nologin
# mail:x:8:8:mail:/var/mail:/usr/sbin/nologin
# news:x:9:9:news:/var/spool/news:/usr/sbin/nologin

# print each line (explicit)
awk &apos;{ print $0 }&apos; /etc/passwd | head
# root:x:0:0:root:/root:/bin/bash
# daemon:x:1:1:daemon:/usr/sbin:/usr/sbin/nologin
# bin:x:2:2:bin:/bin:/usr/sbin/nologin
# sys:x:3:3:sys:/dev:/usr/sbin/nologin
# sync:x:4:65534:sync:/bin:/bin/sync
# games:x:5:60:games:/usr/games:/usr/sbin/nologin
# man:x:6:12:man:/var/cache/man:/usr/sbin/nologin
# lp:x:7:7:lp:/var/spool/lpd:/usr/sbin/nologin
# mail:x:8:8:mail:/var/mail:/usr/sbin/nologin
# news:x:9:9:news:/var/spool/news:/usr/sbin/nologin

# print each line (stdin)
head /etc/passwd | awk &apos;{ print $0 }&apos;
# root:x:0:0:root:/root:/bin/bash
# daemon:x:1:1:daemon:/usr/sbin:/usr/sbin/nologin
# bin:x:2:2:bin:/bin:/usr/sbin/nologin
# sys:x:3:3:sys:/dev:/usr/sbin/nologin
# sync:x:4:65534:sync:/bin:/bin/sync
# games:x:5:60:games:/usr/games:/usr/sbin/nologin
# man:x:6:12:man:/var/cache/man:/usr/sbin/nologin
# lp:x:7:7:lp:/var/spool/lpd:/usr/sbin/nologin
# mail:x:8:8:mail:/var/mail:/usr/sbin/nologin
# news:x:9:9:news:/var/spool/news:/usr/sbin/nologin

# print 1st field at each line (fields separated with spaces by default)
head /etc/passwd | awk &apos;{ print $1 }&apos;
# root:x:0:0:root:/root:/bin/bash
# daemon:x:1:1:daemon:/usr/sbin:/usr/sbin/nologin
# bin:x:2:2:bin:/bin:/usr/sbin/nologin
# sys:x:3:3:sys:/dev:/usr/sbin/nologin
# sync:x:4:65534:sync:/bin:/bin/sync
# games:x:5:60:games:/usr/games:/usr/sbin/nologin
# man:x:6:12:man:/var/cache/man:/usr/sbin/nologin
# lp:x:7:7:lp:/var/spool/lpd:/usr/sbin/nologin
# mail:x:8:8:mail:/var/mail:/usr/sbin/nologin
# news:x:9:9:news:/var/spool/news:/usr/sbin/nologin

# print 1st field at each line (fields separated with &apos;:&apos;)
head /etc/passwd | awk -F: &apos;{ print $1 }&apos;
# root
# daemon
# bin
# sys
# sync
# games
# man
# lp
# mail
# news

# print 1st and 7th fields with given format
head /etc/passwd | awk -F: &apos;{ printf &quot;%-20s%s\n&quot;, $1, $7 }&apos;
# root                /bin/bash
# daemon              /usr/sbin/nologin
# bin                 /usr/sbin/nologin
# sys                 /usr/sbin/nologin
# sync                /bin/sync
# games               /usr/sbin/nologin
# man                 /usr/sbin/nologin
# lp                  /usr/sbin/nologin
# mail                /usr/sbin/nologin
# news                /usr/sbin/nologin

# print 1st field at each line (fields separated with &apos;:&apos; using FS variable)
head /etc/passwd | awk &apos;BEGIN { FS=&quot;:&quot; } { print $1 }&apos;
# root
# daemon
# bin
# sys
# sync
# games
# man
# lp
# mail
# news

# print argument count at the beginning
awk &apos;BEGIN { print ARGC }&apos; /etc/passwd
# 2

# print arguments at the beginning
awk &apos;BEGIN { for (i = 0; i &lt; ARGC; i++) print ARGV[i] }&apos; /etc/passwd
# awk
# /etc/passwd

# print arguments at the beginning (multi-line syntax)
awk &apos;
BEGIN {
    for (i = 0; i &lt; ARGC; i++) {
        print ARGV[i]
    }
}&apos; /etc/passwd
# awk
# /etc/passwd

# print value of &apos;HOME&apos; environment variable
awk &apos;BEGIN { print ENVIRON[&quot;HOME&quot;] }&apos;
# /home/gokce

# print number of records and number of fields (records are lines by default)
head /etc/passwd | awk -F: &apos;{ printf &quot;%d - %d\n&quot;, NR, NF }&apos;
# 1 - 7
# 2 - 7
# 3 - 7
# 4 - 7
# 5 - 7
# 6 - 7
# 7 - 7
# 8 - 7
# 9 - 7
# 10 - 7

# print lines with &apos;bash&apos; pattern (implicit printing)
cat /etc/passwd | awk -F: &apos;/bash/&apos;
# root:x:0:0:root:/root:/bin/bash
# gokce:x:1000:1000:gokce,,,:/home/gokce:/bin/bash

# print lines with &apos;bash&apos; pattern (explicit printing)
cat /etc/passwd | awk -F: &apos;/bash/ { print $0 }&apos;
# root:x:0:0:root:/root:/bin/bash
# gokce:x:1000:1000:gokce,,,:/home/gokce:/bin/bash

# print 1st field of each line with &apos;bash&apos; pattern
cat /etc/passwd | awk -F: &apos;/bash/ { print $1 }&apos;
# root
# gokce

# print 1st field of each line where 7th field is &apos;/bin/bash&apos;
cat /etc/passwd | awk -F: &apos;$7 == &quot;/bin/bash&quot; { print $1 }&apos;
# root
# gokce

# print 1st field of each line from 1st line to 5th line
cat /etc/passwd | awk -F: &apos;NR == 1, NR == 5 { print $1 }&apos;
# root
# daemon
# bin
# sys
# sync

# increment &apos;numlines&apos; at each line and print at the end
cat /etc/passwd | awk &apos;
{ numlines++ }
END { print numlines }&apos;
# 41

# count number of users for each shell
cat /etc/passwd | awk -F: &apos;
{ shell[$7]++ }
END { for (i in shell) printf &quot;%-20s%d\n&quot;, i, shell[i] }&apos;
# /bin/sync           1
# /bin/bash           2
# /bin/false          22
# /usr/sbin/nologin   16

# run external &apos;date&apos; command and read its stdout
awk &apos;BEGIN {
    &quot;date&quot; | getline line
    print line
    close(&quot;date&quot;)
}&apos;
# Thu Oct 13 21:04:58 EEST 2016

# run external &apos;date -u&apos; command and read its stdout
awk &apos;BEGIN {
    cmd = &quot;date -u&quot;
    cmd | getline line
    print line
    close(cmd)
}&apos;
# Thu Oct 13 18:05:03 UTC 2016

# run external &apos;cat&apos; command and write to its stdin
awk &apos;BEGIN {
    print &quot;hello world&quot; | &quot;cat&quot;
    close(&quot;cat&quot;)
}&apos;
# hello world

# run a shell command by writing to &apos;sh&apos; stdin
awk &apos;BEGIN {
    print &quot;ls -l /bin/echo&quot; | &quot;sh&quot;
    close(&quot;sh&quot;)
}&apos;
# -rwxr-xr-x 1 root root 31376 Feb 18  2016 /bin/echo

# run a shell command
awk &apos;BEGIN { system(&quot;ls -l /bin/echo&quot;) }&apos;
# -rwxr-xr-x 1 root root 31376 Feb 18  2016 /bin/echo

# define a &apos;fact&apos; function and use it to calculate factorial of 5
awk &apos;
BEGIN {
    print fact(5)
}

function fact(n) {
    if (n &lt; 2)
        return 1;
    return n * fact(n-1);
}&apos;
# 120

# calculate mean of random numbers
seq 100 | shuf | head | awk &apos;
{ sum += $0 } 
END { print sum / NR }&apos;
# 72.7

# define &apos;mean&apos; as alias (escape &apos;$&apos; characters inside double quotes)
alias mean=&quot;awk &apos;{ sum += \$0 } END { print sum / NR }&apos;&quot;

# use &apos;mean&apos; alias
seq 100 | shuf | head | mean
# 49.2

# calculate stdev of random numbers
seq 100 | shuf | head | awk &apos;
{ sum += $0; sumsq += $0^2 }
END { print sqrt(NR * sumsq - sum^2) / NR; }&apos;
# 29.3966

# define &apos;stdev&apos; as alias (escape &apos;$&apos; characters inside double quotes)
alias stdev=&quot;awk &apos;{ sum += \$0; sumsq += \$0^2 } END { print sqrt(NR * sumsq - sum^2) / NR; }&apos;&quot;

# use &apos;stdev&apos; alias
seq 100 | shuf | head | stdev
# 27.7027

# generate a text with 100 random words from 20 random words
shuf /usr/share/dict/words | head -20 | shuf -r | head -100 | fmt
# updating poignantly poignantly lobotomies Intel&apos;s seesaw&apos;s baas vise&apos;s
# hurray&apos;s Intel&apos;s Grampians Grampians settlement whooshing intertwines
# Terrell lobotomies baas tiny Turing hurray&apos;s lobotomies hurray&apos;s idea&apos;s
# underwriter&apos;s Grampians seesaw&apos;s Turing Intel&apos;s whooshing vise&apos;s idea&apos;s
# Terrell baas underwriter&apos;s whooshing Turing Gorbachev freshets pluckier
# settlement lobotomies baas Terrell settlement pluckier freshets pluckier
# underwriter&apos;s Terrell baas lobotomies pluckier poignantly intertwines
# tiny idea&apos;s settlement pluckier tiny Terrell baas settlement pluckier
# idea&apos;s freshets vise&apos;s updating Grampians underwriter&apos;s settlement
# intertwines hurray&apos;s poignantly vise&apos;s underwriter&apos;s updating baas idea&apos;s
# intertwines Turing underwriter&apos;s underwriter&apos;s Grampians vise&apos;s baas
# Terrell Terrell poignantly Grampians hurray&apos;s seesaw&apos;s pluckier Turing
# lobotomies poignantly Terrell freshets underwriter&apos;s tiny

# word frequency (adapted from &apos;Effective awk Programming&apos; by &apos;Arnold Robbins&apos;)
shuf /usr/share/dict/words | head -20 | shuf -r | head -100 | fmt | awk &apos;
{
    $0 = tolower($0)
    gsub(/[^[:alnum:]_[:blank:]]/, &quot;&quot;, $0)
    for (i = 1; i &lt;= NF; i++) {
        freq[$i]++
        if (maxlen &lt; length($i)) {
            maxlen = length($i)
        }
    }
}

END {
    sort = &quot;sort -k2 -rn | head -n 5&quot;
    for (word in freq) {
        printf &quot;%-*s%d\n&quot;, maxlen, word, freq[word] | sort
    }
    close(sort)
}&apos;
# walmarts    11
# hogs        7
# trousseaux  6
# trees       6
# priming     6
</code></pre>

</body>

</html>
